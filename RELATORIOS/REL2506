      *===============================================================* 00001000
       IDENTIFICATION                                        DIVISION.  00002000
      *===============================================================* 00003000
                                                                        00004000
       PROGRAM-ID.  REL2508.                                            00005000
                                                                        00006000
      *===============================================================* 00007000
      *     PROGRAMA....: REL2508.                                    * 00008000
      *     PROGRAMADOR.: MURIEL ANDRIGUETO                           * 00009000
      *     EMPRESA.....: FOURSYS                                     * 00010000
      *     ANALISTA....: IVAN SANCHES                                * 00010102
      *     DATA........: 27/05/2022                                  * 00011002
      *---------------------------------------------------------------* 00012000
      *     OBJETIVO....: RECEBE OS REGISTROS DE UM ARQUIVO "FUNC"    * 00013002
      *                   (ID, NOME, SETOR, SALARIO) SOMA OS SALARIOS * 00014002
      *                   POR SETOR E SOMA DOS SALARIOS DE TODOS OS   * 00014102
      *                   REGISTROS. O MAXIMO DE LINHAS POR PAGINA    * 00014202
      *                   E DE 15, SENDO FEITA A QUEBRA QUANDO O      * 00014302
      *                   VALOR   ATINGIDO.                           * 00014402
      *---------------------------------------------------------------* 00014503
      *     ARQUIVOS....:                            INCLUDE/BOOK     * 00014603
      *      DDNAME               I/O                                 * 00014704
      *      JCLFUNC               I                   #FUNC          * 00014804
      *      JCLRELAT              O                   #GLOG          * 00014904
      *                                                #CAB             00015004
      *---------------------------------------------------------------* 00015103
      *     MODULOS.....:                                             * 00015203
      *     GRAVALOG - TRATAMENTO DE ERROS                            * 00015303
      *---------------------------------------------------------------* 00015500
                                                                        00015600
      *===============================================================* 00015700
       ENVIRONMENT                                           DIVISION.  00015800
      *===============================================================* 00015900
      *---------------------------------------------------------------* 00016000
       CONFIGURATION                                          SECTION.  00016100
      *---------------------------------------------------------------* 00016200
       SPECIAL-NAMES.                                                   00016300
           DECIMAL-POINT IS COMMA.                                      00016400
                                                                        00016500
                                                                        00016600
       INPUT-OUTPUT                                           SECTION.  00016700
       FILE-CONTROL.                                                    00016800
           SELECT FUNC      ASSIGN TO JCLFUNC                           00016900
               FILE STATUS  IS WRK-FS-FUNC.                             00017000
                                                                        00017100
           SELECT RELAT     ASSIGN TO JCLRELAT                          00017200
               FILE STATUS  IS WRK-FS-RELAT.                            00017300
      *===============================================================* 00017400
       DATA                                                  DIVISION.  00017500
      *===============================================================* 00017600
       FILE                                                   SECTION.  00017700
       FD FUNC                                                          00017800
           RECORDING MODE IS F                                          00017900
           LABEL RECORD IS STANDARD                                     00018000
           BLOCK CONTAINS 0 RECORDS.                                    00018100
                                                                        00018204
      *---------------------------------------------------------------* 00018803
      * ID      05        LRECL 45                                    * 00018903
      * NOME    30                                                    * 00019003
      * SETOR   05                                                    * 00019103
      * SALARIO 06                                                    * 00019203
      *---------------------------------------------------------------* 00019303
                                                                        00019400
          COPY '#FUNC'.                                                 00019500
                                                                        00019600
       FD RELAT                                                         00019700
           RECORDING MODE IS F                                          00019800
           BLOCK CONTAINS 0 RECORDS.                                    00019900
       01 FD-RELAT PIC X(58).                                           00020000
                                                                        00020100
      *---------------------------------------------------------------* 00020200
       WORKING-STORAGE                                        SECTION.  00020300
      *---------------------------------------------------------------* 00020400
                                                                        00020504
      *---------------------------------------------------------------* 00020604
       01 FILLER PIC X(50) VALUE                                        00020704
              '------------------#GLOG--------------------------'.      00020805
      *---------------------------------------------------------------* 00020904
                                                                        00021004
      *---------------------------------------------------------------* 00021103
      * PROGRAMA 08            LRECL 48                               * 00021203
      * SECAO    04                                                   * 00021303
      * MENSAGEM 30                                                   * 00021403
      * STATUS   02                                                   * 00021503
      *---------------------------------------------------------------* 00021603
                                                                        00021700
          COPY '#GLOG'.                                                 00021800
                                                                        00021904
      *---------------------------------------------------------------* 00022004
       01 FILLER PIC X(50) VALUE                                        00022104
              '------------------#CAB---------------------------'.      00022205
      *---------------------------------------------------------------* 00022304
                                                                        00022404
          COPY '#CAB'.                                                  00022500
                                                                        00022604
      *---------------------------------------------------------------* 00022704
       01 FILLER PIC X(50) VALUE                                        00022804
              '------------------#WRKFUNC-----------------------'.      00022905
      *---------------------------------------------------------------* 00023004
                                                                        00023100
          COPY '#WRKFUNC'.                                              00023200
                                                                        00023300
      *---------------------------------------------------------------* 00023404
       01 FILLER PIC X(50) VALUE                                        00023504
              '----------VARIAVEIS DE FILE STATUS---------------'.      00023605
      *---------------------------------------------------------------* 00023704
                                                                        00023804
       77 WRK-FS-RELAT     PIC 9(02).                                   00023900
       77 WRK-FS-FUNC      PIC 9(02).                                   00024000
                                                                        00024104
      *---------------------------------------------------------------* 00024204
       01 FILLER PIC X(50) VALUE                                        00024304
              '-------------VARIAVEIS PARA ACUMULACAO-----------'.      00024405
      *---------------------------------------------------------------* 00024504
                                                                        00024604
       77 WRK-ACUM-LIDOS   PIC 9(02).                                   00024700
       77 WRK-ACUM-SETOR   PIC 9(05)V99 VALUE ZEROS.                    00024800
       77 WRK-SALTOTAL     PIC 9(05)V99 VALUE ZEROS.                    00024900
       77 WRK-TOTLINPAG    PIC 9(02) VALUE 15.                          00025000
                                                                        00025104
      *---------------------------------------------------------------* 00025204
       01 FILLER PIC X(50) VALUE                                        00025304
              '-------VARIAVEIS PARA ACUMULACAO/EXIBICAO--------'.      00025405
      *---------------------------------------------------------------* 00025504
                                                                        00025604
       01 WRK-TOTALSETOR.                                               00025700
         05 FILLER           PIC X(27) VALUE SPACES.                    00025800
         05 FILLER           PIC X(15) VALUE 'TOTAL DO SETOR '.         00025900
         05 WRK-SETOR-ATUAL  PIC 9(04) VALUE ZEROS.                     00026000
         05 FILLER           PIC X(02) VALUE ': '.                      00026100
         05 WRK-SALARIO-ED   PIC $99.999,99.                            00026200
       01 WRK-TOTALFINAL.                                               00026400
         05 FILLER           PIC X(13) VALUE 'TOTAL FINAL: '.           00026500
         05 WRK-SALTOTAL-ED  PIC $99.999,99.                            00026600
                                                                        00026704
       77 WRK-GRAVALOG       PIC X(8) VALUE 'GRAVALOG'.                 00026803
                                                                        00026904
      *---------------------------------------------------------------* 00027004
       01 FILLER PIC X(50) VALUE                                        00027104
              '-------------VARIAVEIS PARA EDICAO---------------'.      00027205
      *---------------------------------------------------------------* 00027304
                                                                        00027404
       77 WRK-LINHAS       PIC 9(03) VALUE 1.                           00027500
       77 WRK-SPACES       PIC X(58) VALUE SPACES.                      00027600
                                                                        00027704
      *---------------------------------------------------------------* 00027804
       01 FILLER PIC X(50) VALUE                                        00027904
              '------------VARIAVEIS PARA MENSAGEM--------------'.      00028005
      *---------------------------------------------------------------* 00028104
                                                                        00028204
       77 WRK-MSG          PIC X(30) VALUE SPACES.                      00028303
       77 WRK-MSGARQVAZIO  PIC X(16) VALUE ' ARQUIVO VAZIO! '.          00028406
       77 WRK-MSGFINALARQ  PIC X(16) VALUE ' FIM DE ARQUIVO '.          00028506
       77 WRK-MSGLIDOS     PIC X(16) VALUE 'LIDOS.......... '.          00028606
                                                                        00028704
      *===============================================================* 00028800
       PROCEDURE                                           DIVISION.    00028900
      *===============================================================* 00029000
      *---------------------------------------------------------------* 00029100
       0000-PRINCIPAL                                         SECTION.  00029200
      *---------------------------------------------------------------* 00029300
             PERFORM 1000-INICIAR.                                      00029404
              PERFORM 1200-TESTA-VAZIO.                                 00029504
             PERFORM 2000-PROCESSAR UNTIL WRK-FS-FUNC EQUAL 10.         00029604
             PERFORM 3000-FINALIZAR.                                    00029704
             STOP RUN.                                                  00029804
      *---------------------------------------------------------------* 00029900
       0000-99-FIM.                                      EXIT.          00030000
      *---------------------------------------------------------------* 00030100
      *---------------------------------------------------------------* 00030200
       1000-INICIAR                                           SECTION.  00030300
      *---------------------------------------------------------------* 00030400
                 OPEN INPUT FUNC                                        00030503
                     OUTPUT RELAT.                                      00030603
                 PERFORM 1100-TESTA-STATUS.                             00030703
      *---------------------------------------------------------------* 00030800
       1000-99-FIM.                                      EXIT.          00030900
      *---------------------------------------------------------------* 00031000
      *---------------------------------------------------------------* 00031100
       1100-TESTA-STATUS                                      SECTION.  00031200
      *---------------------------------------------------------------* 00031300
                 IF WRK-FS-FUNC NOT EQUAL 0                             00031403
                   MOVE 'REL2508'               TO WRK-PROGRAMA         00031503
                   MOVE '1000'                  TO WRK-SECAO            00031603
                   MOVE 'ERRO OPEN FUNC - ' TO WRK-MENSAGEM             00031703
                   MOVE WRK-FS-FUNC             TO WRK-STATUS           00031803
                 PERFORM 9000-TRATA-ERROS                               00031903
                 END-IF.                                                00032003
      *---------------------------------------------------------------* 00032100
       1100-99-FIM.                                      EXIT.          00032200
      *---------------------------------------------------------------* 00032300
      *---------------------------------------------------------------* 00032400
       1200-TESTA-VAZIO                                       SECTION.  00032500
      *---------------------------------------------------------------* 00032600
                 READ FUNC.                                             00032703
                  IF WRK-FS-FUNC EQUAL 10                               00032803
                   MOVE  WRK-MSGARQVAZIO TO WRK-MSG                     00032903
                    DISPLAY WRK-MSG                                     00033003
                     MOVE WRK-MSGFINALARQ TO WRK-MSG                    00033103
                      DISPLAY WRK-MSG                                   00033203
                  ELSE                                                  00033303
                   MOVE FD-SETORFUNC TO WRK-SETOR-ATUAL                 00033403
                    PERFORM 1300-IMPRIME-CABEC                          00033503
                  END-IF.                                               00033603
      *---------------------------------------------------------------* 00033700
       1200-99-FIM.                                      EXIT.          00033800
      *---------------------------------------------------------------* 00033900
      *---------------------------------------------------------------* 00034000
       1300-IMPRIME-CABEC                                     SECTION.  00034100
      *---------------------------------------------------------------* 00034200
                 WRITE FD-RELAT FROM WRK-SPACES                         00034303
                  IF WRK-PAG EQUAL 1                                    00034403
                    WRITE FD-RELAT FROM WRK-CABEC1                      00034503
                  ELSE                                                  00034603
                    WRITE FD-RELAT FROM WRK-CABEC1 AFTER PAGE           00034703
                  END-IF.                                               00034803
                 WRITE FD-RELAT FROM WRK-SPACES                         00034903
                 WRITE FD-RELAT FROM WRK-CABEC2                         00035003
                 ADD 4 TO WRK-LINHAS.                                   00035103
      *---------------------------------------------------------------* 00035200
       1300-99-FIM.                                      EXIT.          00035300
      *---------------------------------------------------------------* 00035400
      *---------------------------------------------------------------* 00035500
       2000-PROCESSAR                                         SECTION.  00035600
      *---------------------------------------------------------------* 00035700
                 ADD 1 TO WRK-ACUM-LIDOS.                               00035800
                 ADD FD-SALFUNC TO WRK-SALTOTAL                         00035900
                 PERFORM 2100-VERIFICA-LINHAS                           00036000
                 PERFORM 2200-VERIFICA-SETOR                            00036800
                 ADD 1 TO WRK-LINHAS.                                   00037000
                 PERFORM 2300-MUDA-PAG                                  00038500
                 READ FUNC.                                             00038700
                                                                        00038800
                 IF WRK-FS-FUNC EQUAL 10                                00038900
                   PERFORM 2500-IMPRIME-SUBTOTAL                        00039000
                    PERFORM 2600-IMPRIME-TOTALFINAL                     00039103
                 END-IF.                                                00039300
      *---------------------------------------------------------------* 00039400
       2000-99-FIM.                                      EXIT.          00039500
      *---------------------------------------------------------------* 00039600
      *---------------------------------------------------------------* 00039800
       2100-VERIFICA-LINHAS                                  SECTION.   00039900
      *---------------------------------------------------------------* 00040000
                 IF FD-SETORFUNC EQUAL WRK-SETOR-ATUAL AND              00040100
                   WRK-LINHAS GREATER WRK-TOTLINPAG - 2                 00040200
                    ADD 1 TO WRK-PAG                                    00040300
                     MOVE 1 TO WRK-LINHAS                               00040400
                      PERFORM 1300-IMPRIME-CABEC                        00040500
                 END-IF.                                                00040600
      *---------------------------------------------------------------* 00040700
       2100-99-FIM.                                      EXIT.          00040800
      *---------------------------------------------------------------* 00040900
      *---------------------------------------------------------------* 00041100
       2200-VERIFICA-SETOR                                   SECTION.   00041200
      *---------------------------------------------------------------* 00041300
                 IF FD-SETORFUNC EQUAL WRK-SETOR-ATUAL                  00041400
                   ADD FD-SALFUNC TO WRK-ACUM-SETOR                     00041500
                    PERFORM 2400-IMPRIME-REGISTROS                      00041600
                 ELSE                                                   00041700
                   PERFORM 2500-IMPRIME-SUBTOTAL                        00041800
                    ADD 1 TO WRK-LINHAS                                 00041913
                     ADD FD-SALFUNC TO WRK-ACUM-SETOR                   00042000
                      PERFORM 2400-IMPRIME-REGISTROS                    00042100
                       MOVE FD-SETORFUNC TO WRK-SETOR-ATUAL             00042200
                 END-IF.                                                00042300
      *---------------------------------------------------------------* 00042400
       2200-99-FIM.                                      EXIT.          00042500
      *---------------------------------------------------------------* 00042600
      *---------------------------------------------------------------* 00042800
       2300-MUDA-PAG                                         SECTION.   00042900
      *---------------------------------------------------------------* 00043000
                 IF WRK-LINHAS GREATER WRK-TOTLINPAG                    00044000
                     PERFORM 2500-IMPRIME-SUBTOTAL                      00044100
                      ADD 1 TO WRK-PAG                                  00044200
                       MOVE 1 TO WRK-LINHAS                             00044300
                        PERFORM 1300-IMPRIME-CABEC                      00044400
                 END-IF.                                                00044500
      *---------------------------------------------------------------* 00044600
       2300-99-FIM.                                      EXIT.          00044700
      *---------------------------------------------------------------* 00044800
      *---------------------------------------------------------------* 00045000
       2400-IMPRIME-REGISTROS                                 SECTION.  00045100
      *---------------------------------------------------------------* 00045200
                 MOVE FD-IDFUNC    TO WRK-IDFUNC                        00045300
                 MOVE FD-NOMEFUNC  TO WRK-NOMEFUNC                      00045400
                 MOVE FD-SETORFUNC TO WRK-SETORFUNC                     00045500
                 MOVE FD-SALFUNC   TO WRK-SALFUNC                       00045600
                  WRITE FD-RELAT FROM WRK-FUNC.                         00045700
      *---------------------------------------------------------------* 00045800
       2400-99-FIM.                                      EXIT.          00045900
      *---------------------------------------------------------------* 00046000
                                                                        00046100
      *---------------------------------------------------------------* 00046200
       2500-IMPRIME-SUBTOTAL                                  SECTION.  00046300
      *---------------------------------------------------------------* 00046400
                 MOVE WRK-ACUM-SETOR TO WRK-SALARIO-ED                  00046500
                 WRITE FD-RELAT FROM WRK-TOTALSETOR                     00046600
                 WRITE FD-RELAT FROM WRK-SPACES                         00046700
                 MOVE 0 TO WRK-ACUM-SETOR.                              00046800
      *---------------------------------------------------------------* 00046900
       2500-99-FIM.                                      EXIT.          00047000
      *---------------------------------------------------------------* 00047100
      *---------------------------------------------------------------* 00047301
       2600-IMPRIME-TOTALFINAL                                SECTION.  00047401
      *---------------------------------------------------------------* 00047501
                 IF WRK-LINHAS GREATER WRK-TOTLINPAG - 2                00047612
                   ADD 1 TO WRK-PAG                                     00047710
                    MOVE 1 TO WRK-LINHAS                                00047810
                     PERFORM 1300-IMPRIME-CABEC                         00047910
                      GO TO 2600-IMPRIME-TOTALFINAL                     00048010
                 ELSE                                                   00048610
                   MOVE WRK-SALTOTAL TO WRK-SALTOTAL-ED                 00048710
                    WRITE FD-RELAT FROM WRK-TOTALFINAL                  00048810
                 END-IF.                                                00049310
      *---------------------------------------------------------------* 00049401
       2600-99-FIM.                                      EXIT.          00049501
      *---------------------------------------------------------------* 00049601
      *---------------------------------------------------------------* 00049700
       3000-FINALIZAR                                         SECTION.  00049800
      *---------------------------------------------------------------* 00049900
                 CLOSE FUNC RELAT.                                      00050003
                 MOVE WRK-MSGLIDOS TO WRK-MSG                           00050103
                 DISPLAY WRK-MSG                                        00050203
                 MOVE WRK-MSGFINALARQ TO WRK-MSG                        00050303
                 DISPLAY WRK-MSG.                                       00050403
      *---------------------------------------------------------------* 00050500
       3000-99-FIM.                                      EXIT.          00050600
      *---------------------------------------------------------------* 00050700
      *---------------------------------------------------------------* 00050800
       9000-TRATA-ERROS                                       SECTION.  00050900
      *---------------------------------------------------------------* 00051000
                 CALL WRK-GRAVALOG USING WRK-DADOS.                     00051103
                 GOBACK.                                                00051200
      *---------------------------------------------------------------* 00051300
       9000-99-FIM.                                      EXIT.          00052000
      *---------------------------------------------------------------* 00060000
