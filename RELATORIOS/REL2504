      *===============================================================* 00001000
       IDENTIFICATION                                        DIVISION.  00002000
      *===============================================================* 00003000
                                                                        00004000
       PROGRAM-ID.  REL2504.                                            00005000
                                                                        00006000
      *===============================================================* 00007000
      *     PROGRAMA....: REL2504.                                    * 00008000
      *     PROGRAMADOR.: MURIEL ANDRIGUETO                           * 00009000
      *     EMPRESA.....: FOURSYS                                     * 00010000
      *     DATA........: 24/05/2022                                  * 00011000
      *---------------------------------------------------------------* 00012000
      *     OBJETIVO....: REVISAO SOBRE ABERTURA E LEITURA DE         * 00013000
      *                   ARQUIVOS SEQUENCIAIS COBOL.                 * 00014000
      *---------------------------------------------------------------* 00015000
                                                                        00015100
      *===============================================================* 00015200
       ENVIRONMENT                                           DIVISION.  00015300
      *===============================================================* 00015400
      *---------------------------------------------------------------* 00015500
       CONFIGURATION                                          SECTION.  00015600
      *---------------------------------------------------------------* 00015700
       SPECIAL-NAMES.                                                   00015800
           DECIMAL-POINT IS COMMA.                                      00015900
                                                                        00016000
                                                                        00016100
       INPUT-OUTPUT                                           SECTION.  00016200
       FILE-CONTROL.                                                    00016300
           SELECT FUNC      ASSIGN TO JCLFUNC                           00016400
               FILE STATUS  IS WRK-FS-FUNC.                             00016500
      *===============================================================* 00016600
       DATA                                                  DIVISION.  00016700
      *===============================================================* 00016800
       FILE                                                   SECTION.  00016900
       FD FUNC                                                          00017000
           RECORDING MODE IS F                                          00018000
           LABEL RECORD IS STANDARD                                     00019000
           BLOCK CONTAINS 0 RECORDS.                                    00019100
                                                                        00019200
      *-------------------------------------------------------------    00019300
      * ID      05        LRECL 45                                      00019400
      * NOME    30                                                      00019500
      * SETOR   05                                                      00019600
      * SALARIO 06                                                      00019700
      *-------------------------------------------------------------    00019800
                                                                        00019900
          COPY '#FUNC'.                                                 00020000
                                                                        00020100
      *---------------------------------------------------------------* 00020200
       WORKING-STORAGE                                        SECTION.  00020300
      *---------------------------------------------------------------* 00020400
                                                                        00020500
      *-------------------------------------------------------------    00020600
      * PROGRAMA 08        LRECL 58                                     00020700
      * SECAO    04                                                     00020800
      * MENSAGEM 30                                                     00020900
      * STATUS   02                                                     00021000
      *-------------------------------------------------------------    00021100
                                                                        00021200
          COPY '#GLOG'.                                                 00021300
                                                                        00021400
          COPY '#CAB'.                                                  00021500
                                                                        00021600
      *-------------------------------------------------------------    00021700
      * WRK-IDFUNC    X(05)    LRECL 58                                 00021800
      * FILLER        X(04)                                             00021900
      * WRK-NOMEFUNC  X(30)                                             00022000
      * WRK-SETORFUNC 9(04)                                             00022100
      * FILLER        X(08)                                             00022200
      * WRK-SALFUNC   9(04)V99                                          00022300
      * FILLER        X(01)                                             00022400
      *-------------------------------------------------------------    00022500
                                                                        00022600
          COPY '#WRKFUNC'.                                              00022700
                                                                        00022800
      *---------------------VALORES PARA ACUMULACAO-----------------    00022900
                                                                        00023000
       77 WRK-FS-FUNC      PIC 9(02).                                   00024000
       77 WRK-ACUM-LIDOS   PIC 9(02).                                   00024100
       77 WRK-ACUM-SALARIO PIC 9(05)V99 VALUE ZEROS.                    00024200
       77 WRK-SALTOTAL     PIC 9(05)V99 VALUE ZEROS.                    00024300
                                                                        00024400
      *---------------------VALORES EDICAO--------------------------    00024500
                                                                        00024600
       77 WRK-SALARIO-ED   PIC $99.999,99.                              00024715
       77 WRK-LINHAS       PIC 9(03) VALUE 1.                           00024815
       77 WRK-SETOR-ATUAL  PIC 9(04) VALUE ZEROS.                       00024915
       77 WRK-SPACES       PIC X(49) VALUE SPACES.                      00025015
                                                                        00025100
      *===============================================================* 00026000
       PROCEDURE                                           DIVISION.    00026100
      *===============================================================* 00026200
      *---------------------------------------------------------------* 00026300
       0000-PRINCIPAL                                         SECTION.  00026400
      *---------------------------------------------------------------* 00026500
           PERFORM 1000-INICIAR.                                        00026600
           PERFORM 2000-PROCESSAR UNTIL WRK-FS-FUNC EQUAL 10.           00026700
           PERFORM 3000-FINALIZAR.                                      00026800
           STOP RUN.                                                    00026900
      *---------------------------------------------------------------* 00027000
       0000-99-FIM.                                      EXIT.          00027100
      *---------------------------------------------------------------* 00027200
                                                                        00027300
      *---------------------------------------------------------------* 00027400
       1000-INICIAR                                           SECTION.  00027500
      *---------------------------------------------------------------* 00027600
           OPEN INPUT FUNC.                                             00027700
             PERFORM 1100-TESTA-STATUS.                                 00027800
             PERFORM 1200-TESTA-VAZIO.                                  00028000
      *---------------------------------------------------------------* 00028100
       1000-99-FIM.                                      EXIT.          00028200
      *---------------------------------------------------------------* 00028300
                                                                        00028400
      *---------------------------------------------------------------* 00028500
       1100-TESTA-STATUS                                      SECTION.  00028600
      *---------------------------------------------------------------* 00028700
           IF WRK-FS-FUNC NOT EQUAL 0                                   00028806
            MOVE 'REL2504'               TO WRK-PROGRAMA                00028900
            MOVE '1000'                  TO WRK-SECAO                   00029000
            MOVE 'ERRO OPEN FUNC - ' TO WRK-MENSAGEM                    00030000
            MOVE WRK-FS-FUNC             TO WRK-STATUS                  00030100
               PERFORM 9000-TRATA-ERROS                                 00030200
           END-IF.                                                      00030300
      *---------------------------------------------------------------* 00030400
       1100-99-FIM.                                      EXIT.          00030500
      *---------------------------------------------------------------* 00030600
      *---------------------------------------------------------------* 00030700
       1200-TESTA-VAZIO                                       SECTION.  00030800
      *---------------------------------------------------------------* 00030900
             READ FUNC.                                                 00031000
             IF WRK-FS-FUNC EQUAL 10                                    00031100
              DISPLAY ' ARQUIVO VAZIO! '                                00031200
              DISPLAY ' FIM DE ARQUIVO '                                00031300
             ELSE                                                       00031500
              PERFORM 1300-IMPRIME-CABEC                                00031600
             END-IF.                                                    00031700
      *---------------------------------------------------------------* 00031800
       1200-99-FIM.                                      EXIT.          00031900
      *---------------------------------------------------------------* 00032000
                                                                        00032100
      *---------------------------------------------------------------* 00033000
       1300-IMPRIME-CABEC                                     SECTION.  00033100
      *---------------------------------------------------------------* 00033200
              DISPLAY ' '                                               00033413
              DISPLAY WRK-CABEC1                                        00033513
              DISPLAY ' '                                               00033613
              DISPLAY WRK-CABEC2                                        00033713
              ADD 4 TO WRK-LINHAS.                                      00033812
      *---------------------------------------------------------------* 00034200
       1300-99-FIM.                                      EXIT.          00034300
      *---------------------------------------------------------------* 00034400
                                                                        00034500
      *---------------------------------------------------------------* 00034600
       2000-PROCESSAR                                         SECTION.  00034700
      *---------------------------------------------------------------* 00034800
                                                                        00034933
                ADD 1 TO WRK-ACUM-LIDOS.                                00036033
                ADD FD-SALFUNC TO WRK-ACUM-SALARIO                      00036133
                ADD FD-SALFUNC TO WRK-SALTOTAL                          00036233
                                                                        00036316
                MOVE FD-IDFUNC    TO WRK-IDFUNC.                        00036401
                MOVE FD-NOMEFUNC  TO WRK-NOMEFUNC.                      00036501
                MOVE FD-SETORFUNC TO WRK-SETORFUNC.                     00036618
                MOVE FD-SALFUNC   TO WRK-SALFUNC.                       00036701
                DISPLAY WRK-LINHAS ' - ' WRK-FUNC.                      00036801
                ADD 1 TO WRK-LINHAS.                                    00036901
                                                                        00037030
                IF WRK-LINHAS GREATER 8                                 00037832
                     PERFORM 2100-IMPRIME-SUBTOTAL                      00037933
                     ADD 1 TO WRK-PAG                                   00038011
                     MOVE 1 TO WRK-LINHAS                               00038107
                     PERFORM 1300-IMPRIME-CABEC                         00038216
                END-IF.                                                 00038313
                                                                        00038410
                READ FUNC.                                              00038510
                                                                        00038613
                 IF WRK-FS-FUNC EQUAL 10                                00038711
                   PERFORM 2100-IMPRIME-SUBTOTAL                        00038833
                   DISPLAY WRK-SPACES                                   00038911
                 END-IF.                                                00039011
      *---------------------------------------------------------------* 00039100
       2000-99-FIM.                                      EXIT.          00039200
      *---------------------------------------------------------------* 00040000
                                                                        00040413
      *---------------------------------------------------------------* 00040513
       2100-IMPRIME-SUBTOTAL                                  SECTION.  00040633
      *---------------------------------------------------------------* 00040713
                    MOVE WRK-ACUM-SALARIO TO WRK-SALARIO-ED             00040911
                    DISPLAY WRK-SPACES                                  00041011
                    DISPLAY WRK-SPACES 'TOTAL: ' WRK-SALARIO-ED         00041111
                    MOVE 0 TO WRK-ACUM-SALARIO.                         00041212
      *---------------------------------------------------------------* 00041413
       2100-99-FIM.                                      EXIT.          00041533
      *---------------------------------------------------------------* 00041613
                                                                        00041713
      *---------------------------------------------------------------* 00041800
       3000-FINALIZAR                                         SECTION.  00041900
      *---------------------------------------------------------------* 00042000
           CLOSE FUNC.                                                  00042100
           MOVE WRK-SALTOTAL TO WRK-SALARIO-ED.                         00042200
           DISPLAY ' '                                                  00042300
           DISPLAY 'LIDOS............ ' WRK-ACUM-LIDOS.                 00042400
           DISPLAY 'TOTAL SALARIOS... ' WRK-SALARIO-ED.                 00042500
           DISPLAY 'FIM DE ARQUIVO... '.                                00042600
      *---------------------------------------------------------------* 00042700
       3000-99-FIM.                                      EXIT.          00042800
      *---------------------------------------------------------------* 00042900
                                                                        00043000
      *---------------------------------------------------------------* 00043100
       9000-TRATA-ERROS                                       SECTION.  00043200
      *---------------------------------------------------------------* 00043300
           CALL 'GRAVALOG' USING WRK-DADOS.                             00043400
            GOBACK.                                                     00043500
      *---------------------------------------------------------------* 00043600
       9000-99-FIM.                                      EXIT.          00044000
      *---------------------------------------------------------------* 00050000
