      *===============================================================*
       IDENTIFICATION                                        DIVISION.
      *===============================================================*

       PROGRAM-ID. F25CIP2.

      *===============================================================*
      *   AUTOR.....:MURIEL ANDRIGUETO                                *
      *   INSTRUTOR.:IVAN PETRUCCI                                    *
      *   DATA .....:28/06/2022                                       *
      *---------------------------------------------------------------*
      *   OBJETIVO:EXECUCAO DE UM VSAM DE CONSULTA DE FUNCIONARIOS    *
      *                                                               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *   ARQUIVOS:                                                   *
      *   DDNAME......                                                *
      *    DDNAME              I/O                 INCLUDE/BOOK       *
      *---------------------------------------------------------------*
      *   MODULOS....:                             INCLUDE/BOOK       *
      *===============================================================*
      *=============================================================*
       ENVIRONMENT                               DIVISION.
      *=============================================================*
      *=============================================================*
       CONFIGURATION                               SECTION.
      *=============================================================*
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *===============================================================*
       DATA                                      DIVISION.
      *===============================================================*

      *===============================================================*
       WORKING-STORAGE                            SECTION.
      *===============================================================*

           COPY F25CIM1.

      *-------------------------------------------------------------*
       01 FILLER              PIC X(70) VALUE
              '-------VARIAVEIS DE RECEBIMENTO DE DADOS----'.
      *-------------------------------------------------------------*
       01 WRK-DADOS.
          05 WRK-CHAVE     PIC 9(05).
          05 WRK-NOME      PIC X(30).
          05 WRK-SETOR     PIC X(04).
      *-----SALARIO-----
          05 WRK-SALARIO.
           09 WRK-SALMILH   PIC 9(02).
           09 WRK-SALMIL    PIC 9(03).
           09 WRK-SALCENT   PIC 9(03).
           09 WRK-SALDEC    PIC 9(02).
      *-------DATA------
          05 WRK-DATADIA   PIC X(02).
          05 FILLER        PIC X(01) VALUE '/'.
          05 WRK-DATAMES   PIC X(02).
          05 FILLER        PIC X(01) VALUE '/'.
          05 WRK-DATAANO   PIC X(04).
          05 WRK-EMAIL     PIC X(40).

       77 WRK-RESP  PIC S9(04) COMP.
       77 WRK-RET-MAPA PIC S9(04) COMP.
       77 WRK-FLAG     PIC 9(1).

      *---------------------------------------------------------------*
       01 FILLER              PIC X(70) VALUE
              '-------VARIAVEIS DE MASCARA-----------------'.
      *---------------------------------------------------------------*
       77 WRK-SALARIOED     PIC ZZ.ZZZ.ZZ9,99.

      *---------------------------------------------------------------*
       01 FILLER              PIC X(70) VALUE
              '-------VARIAVEIS PARA MENSAGEM--------------'.
      *---------------------------------------------------------------*
       77 WRK-MSGERROSAL PIC X(43) VALUE
                         'REGISTRO ENCONTRADO - SALARIO INCONSISTENTE'.
       77 WRK-REGENC     PIC X(18) VALUE 'REGISTO ENCONTRADO'.
       77 WRK-REGNAOENC  PIC X(23) VALUE 'REGISTRO NAO ENCONTRADO'.
       77 WRK-MSGERRO    PIC X(05) VALUE 'ERRO:'.
       77 WRK-SAIDA      PIC X(30) VALUE '---- FIM DE PROGRAMA'.

      *===============================================================*
       PROCEDURE                                 DIVISION.
      *===============================================================*

      *---------------------------------------------------------------*
       0000-PRINCIPAL                            SECTION.
      *---------------------------------------------------------------*
            PERFORM 1000-INICIAR.
             IF WRK-FLAG EQUAL 1
               PERFORM 2000-PROCESSAR
             END-IF.
            PERFORM 3000-FINALIZAR.

            EXEC CICS
              RETURN TRANSID('T252')
            END-EXEC.
      *---------------------------------------------------------------*
       0000-99-FIM.                              EXIT.
      *---------------------------------------------------------------*

      *---------------------------------------------------------------*
       1000-INICIAR                              SECTION.
      *---------------------------------------------------------------*
            EXEC CICS RECEIVE
               MAPSET('F25CIM1')
               MAP('MAPA01')
               INTO(MAPA01I)
               RESP(WRK-RET-MAPA)
            END-EXEC.

            IF WRK-RET-MAPA EQUAL DFHRESP(MAPFAIL)
              MOVE 2 TO WRK-FLAG

            ELSE
              MOVE 1 TO WRK-FLAG
            END-IF.
      *---------------------------------------------------------------*
       1000-99-FIM.                              EXIT.
      *---------------------------------------------------------------*

      *---------------------------------------------------------------*
       2000-PROCESSAR                            SECTION.
      *---------------------------------------------------------------*

            IF EIBAID = ':'
               EXEC CICS SEND
                 FROM(WRK-SAIDA)
                 ERASE
               END-EXEC

               EXEC CICS
                 RETURN
               END-EXEC
            END-IF.

            IF EIBAID = ''''
              INITIALIZE MAPA01O
            END-IF.


            IF EIBAID = '5'
                MOVE IDI TO WRK-CHAVE
                EXEC CICS READ
                          FILE('FUNC')
                          RIDFLD(WRK-CHAVE)
                          INTO(WRK-DADOS)
                          RESP(WRK-RESP)
                END-EXEC

                 EVALUATE WRK-RESP
                   WHEN DFHRESP(NORMAL)
                    MOVE WRK-NOME      TO NOMEO
                    MOVE WRK-SETOR     TO SETORO
                    MOVE WRK-SALMILH   TO SALMILHO
                    MOVE WRK-SALMIL    TO SALMILO
                    MOVE WRK-SALCENT   TO SALCENTO
                    MOVE WRK-SALDEC    TO SALDECO
                    MOVE WRK-DATADIA   TO DATADIAO
                    MOVE WRK-DATAMES   TO DATAMESO
                    MOVE WRK-DATAANO   TO DATAANOO
                    MOVE WRK-EMAIL     TO EMAILO
                     IF WRK-SALARIO EQUAL ZEROS
                      MOVE WRK-MSGERROSAL TO MSGO
                     ELSE
                      MOVE WRK-REGENC    TO MSGO
                     END-IF
                   WHEN DFHRESP(NOTFND)
                      MOVE WRK-REGNAOENC TO MSGO
                   WHEN OTHER
                    MOVE WRK-MSGERRO TO MSGO
                    MOVE WRK-RESP TO MSGO(08:04)
                 END-EVALUATE
            END-IF.
      *---------------------------------------------------------------*
       2000-99-FIM.                              EXIT.
      *---------------------------------------------------------------*

      *---------------------------------------------------------------*
       3000-FINALIZAR                            SECTION.
      *---------------------------------------------------------------*
            IF WRK-FLAG EQUAL 2
               EXEC CICS SEND
                  MAPSET('F25CIM1')
                  MAP('MAPA01')
                   ERASE
                  MAPONLY
               END-EXEC
            ELSE
                 EXEC CICS SEND
                    MAPSET('F25CIM1')
                    MAP('MAPA01')
                    DATAONLY
                    FROM(MAPA01O)
                 END-EXEC
            END-IF.
      *---------------------------------------------------------------*
       3000-99-FIM.                              EXIT.
      *---------------------------------------------------------------*
