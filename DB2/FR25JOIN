      *=============================================================*   00001000
       IDENTIFICATION                            DIVISION.              00002000
      *=============================================================*   00003000
                                                                        00004000
       PROGRAM-ID. FR25JOIN.                                            00005002
                                                                        00006000
      *=============================================================*   00007000
      *   AUTOR....:MURIEL ANDRIGUETO                               *   00007100
      *   ANALISTA.:IVAN PETRUCCI                                   *   00007200
      *   DATA ....:14/06/2022                                      *   00007301
      *-------------------------------------------------------------*   00007400
      *   OBJETIVO:LER FUNCIONARIOS COM ID ANALITICO DAS            *   00007509
      *            TABELAS RELACIONADAS IVAN.FUNC E IVAN.BENEF      *   00007607
      *            IMPRIMINDO OS RESULTADOS NO ARQUIVO              *   00007709
      *            DE SAIDA SRELBENE                                *   00007802
      *                                                             *   00007900
      *-------------------------------------------------------------*   00008000
      *   BASE DE DADOS:                                            *   00008100
      *   TABELA.DB2..                                              *   00008200
      *    ------              I/O                 INCLUDE/BOOK     *   00008309
      *   IVAN.FUNC             I                  #BKFUNC----      *   00008409
      *   IVAN.BENEF            I                  #BKBENEF---      *   00008509
      *-------------------------------------------------------------*   00008600
      *   ARQUIVOS:                                                 *   00008709
      *    DDNAME              I/O                  COPY/BOOK       *   00008809
      *   SRELBENE              O                  -----------      *   00008909
      *=============================================================*   00009009
                                                                        00009109
      *=============================================================*   00009200
       ENVIRONMENT                               DIVISION.              00009300
      *=============================================================*   00009400
                                                                        00009500
      *-------------------------------------------------------------*   00009609
       CONFIGURATION                               SECTION.             00009700
      *-------------------------------------------------------------*   00009809
       SPECIAL-NAMES.                                                   00009900
           DECIMAL-POINT IS COMMA.                                      00010000
                                                                        00010100
       INPUT-OUTPUT                                 SECTION.            00010200
       FILE-CONTROL.                                                    00010300
            SELECT SRELBENE ASSIGN TO SRELBENE                          00010402
                FILE STATUS IS WRK-FS-SRELBENE.                         00010502
      *=============================================================*   00010600
       DATA                                      DIVISION.              00010700
      *=============================================================*   00010800
       FILE                                        SECTION.             00010900
       FD SRELBENE                                                      00011002
           RECORDING MODE IS F                                          00011100
           LABEL RECORD IS STANDARD                                     00011200
           BLOCK CONTAINS 0 RECORDS.                                    00011300
                                                                        00011400
       01 FD-SRELBENE       PIC X(68).                                  00011502
      *=============================================================*   00011600
       WORKING-STORAGE                             SECTION.             00011700
      *=============================================================*   00011800
                                                                        00011900
           EXEC SQL                                                     00012000
              INCLUDE #BKFUNC                                           00012100
           END-EXEC.                                                    00012200
                                                                        00012300
           EXEC SQL                                                     00012400
              INCLUDE #BKBENEF                                          00012502
           END-EXEC.                                                    00012600
                                                                        00012700
           EXEC SQL                                                     00012800
              INCLUDE SQLCA                                             00012900
           END-EXEC.                                                    00013000
                                                                        00013100
           EXEC SQL                                                     00013200
              DECLARE CFUNC CURSOR FOR                                  00014000
               SELECT ID,NOME,SALARIO,PLANMED,PLANDENT,COTAS            00015002
                FROM IVAN.FUNC F , IVAN.BENEF B                         00016002
                 WHERE F.ID = B.IDFUNC                                  00017003
              ORDER BY ID                                               00018011
           END-EXEC.                                                    00019000
                                                                        00019107
      *-------------------------------------------------------------*   00019206
       01 FILLER              PIC X(70) VALUE                           00019307
              '-------VARIAVEIS DE RECEBIMENTO DE DADOS----'.           00019407
      *-------------------------------------------------------------*   00019506
      *--------LRECL 68---------------------------------------------*   00021002
                                                                        00021106
       01 WRK-SRELBENE.                                                 00022002
          05 WRK-ID           PIC 99999.                                00023000
          05 WRK-NOME         PIC X(30).                                00023100
          05 WRK-SALARIO      PIC 9999999999.                           00023200
          05 WRK-PLANMED      PIC X(10).                                00023302
          05 WRK-PLANDENT     PIC X(10).                                00023402
          05 WRK-COTAS        PIC 999.                                  00023505
                                                                        00023600
      *-------------------------------------------------------------*   00023706
       01 FILLER              PIC X(70) VALUE                           00023807
              '-------VARIAVEIS DE ACUMULACAO-----------------'.        00023906
      *-------------------------------------------------------------*   00024006
                                                                        00024106
       77 WRK-REGLIDOS        PIC 9(03).                                00024200
       77 WRK-SALARIO-ACUM    PIC S9(8)V9(2) COMP.                      00024300
       77 WRK-MAIOR-SALARIO   PIC S9(8)V9(2) COMP.                      00024400
       77 WRK-MAIORVALOR      PIC S9(8)V9(2) COMP.                      00024506
       77 WRK-MEDIA           PIC S9(8)V9(2) COMP.                      00024600
       77 WRK-REGGRAVADOS     PIC 9(03) VALUE ZEROS.                    00024706
                                                                        00024806
      *-------------------------------------------------------------*   00024906
       01 FILLER              PIC X(70) VALUE                           00025007
              '-------VARIAVEIS DE STATUS----------'.                   00025106
      *-------------------------------------------------------------*   00025206
                                                                        00025306
       77 WRK-SQLCODE         PIC -999.                                 00025400
       77 WRK-FS-SRELBENE     PIC 9(02).                                00025605
                                                                        00025706
      *-------------------------------------------------------------*   00025806
       01 FILLER              PIC X(70) VALUE                           00025906
              '---------VARIAVEIS DE MENSAGENS-----'.                   00026006
      *-------------------------------------------------------------*   00026106
                                                                        00026206
       77 WRK-MSGERRO         PIC X(05) VALUE 'ERRO '.                  00026306
       77 WRK-MSGERROOPEN     PIC X(17) VALUE 'NO OPEN DO CURSOR'.      00026406
       77 WRK-MSGSEMFUNC      PIC X(16) VALUE 'SEM FUNCIONARIOS'.       00026506
       77 WRK-MSGERROARQ      PIC X(25) VALUE                           00026606
                                       ' ERRO NO OPEN DO ARQUIVO'.      00026708
       77 WRK-MSGFIMARQ       PIC X(16) VALUE 'FINAL DE ARQUIVO'.       00026806
       77 WRK-MSGERROLEI      PIC X(18) VALUE 'ERRO NA LEITURA - '.     00026906
       77 WRK-MSGREGLIDOS     PIC X(22) VALUE                           00027006
                                        'REGISTROS LIDOS.......'.       00027106
       77 WRK-MSGREGGRAVADOS  PIC X(22) VALUE                           00027206
                                        'REGISTROS GRAVADOS....'.       00027306
       77 WRK-MSGMAIORSAL     PIC X(22) VALUE                           00027406
                                        'MAIOR SALARIO.........'.       00027506
       77 WRK-MSGSALACUM      PIC X(22) VALUE                           00027606
                                        'SALARIO ACUMULADO.....'.       00027706
       77 WRK-MSGMEDIASAL     PIC X(22) VALUE                           00027806
                                        'MEDIA DOS SALARIOS....'.       00027906
       77 WRK-MSGFIMPROG      PIC X(22) VALUE                           00028008
                                        '---FIM DO PROGRAMA--- '.       00028106
                                                                        00028606
      *=============================================================*   00029300
       PROCEDURE DIVISION.                                              00029400
      *=============================================================*   00029500
      *-------------------------------------------------------------*   00029700
       0000-PRINCIPAL                           SECTION.                00029800
      *-------------------------------------------------------------*   00029900
            PERFORM  1000-INICIAR.                                      00030000
            PERFORM  2000-PROCESSAR UNTIL SQLCODE EQUAL 100.            00030100
            PERFORM  3000-FINALIZAR.                                    00030200
            STOP RUN.                                                   00030300
      *-------------------------------------------------------------*   00030400
       0000-99-FIM.              EXIT.                                  00030500
      *-------------------------------------------------------------*   00030600
                                                                        00030706
      *-------------------------------------------------------------*   00030800
       1000-INICIAR                             SECTION.                00030900
      *-------------------------------------------------------------*   00031000
            EXEC SQL                                                    00031100
               OPEN CFUNC                                               00031200
            END-EXEC.                                                   00031300
            PERFORM 1100-VERIFICA-SQL.                                  00031506
            PERFORM 1200-VERIFICA-ARQUIVO.                              00032608
      *-------------------------------------------------------------*   00033100
       1000-99-FIM.              EXIT.                                  00033200
      *-------------------------------------------------------------*   00033300
                                                                        00033406
      *-------------------------------------------------------------*   00033506
       1100-VERIFICA-SQL                        SECTION.                00033606
      *-------------------------------------------------------------*   00033706
            EVALUATE SQLCODE                                            00034106
             WHEN 0                                                     00034206
               PERFORM 1300-LER-FUNC                                    00034306
             WHEN 100                                                   00034406
               DISPLAY WRK-MSGSEMFUNC                                   00034506
             WHEN OTHER                                                 00034606
               MOVE SQLCODE TO WRK-SQLCODE                              00034706
                DISPLAY WRK-MSGERRO WRK-SQLCODE WRK-MSGERROOPEN         00034806
                 STOP RUN                                               00034906
            END-EVALUATE.                                               00035006
      *-------------------------------------------------------------*   00035806
       1100-99-FIM.              EXIT.                                  00035906
      *-------------------------------------------------------------*   00036006
                                                                        00036106
      *-------------------------------------------------------------*   00036206
       1200-VERIFICA-ARQUIVO                    SECTION.                00036306
      *-------------------------------------------------------------*   00036406
            OPEN OUTPUT SRELBENE.                                       00036506
            IF WRK-FS-SRELBENE NOT EQUAL 0                              00036606
               DISPLAY WRK-MSGERROARQ                                   00036706
                  STOP RUN                                              00036806
            END-IF.                                                     00036906
      *-------------------------------------------------------------*   00037806
       1200-99-FIM.              EXIT.                                  00037906
      *-------------------------------------------------------------*   00038006
                                                                        00038106
      *-------------------------------------------------------------*   00038206
       1300-LER-FUNC                                SECTION.            00038306
      *-------------------------------------------------------------*   00038406
            EXEC SQL                                                    00038506
             FETCH CFUNC                                                00038606
              INTO :DB2-ID,                                             00038706
                   :DB2-NOME,                                           00038806
                   :DB2-SALARIO,                                        00038906
                   :DB2-PLANMED,                                        00039006
                   :DB2-PLANDENT,                                       00039106
                   :DB2-COTAS                                           00039206
             END-EXEC.                                                  00039306
            PERFORM 1400-VERIFICA-FUNC.                                 00040408
      *-------------------------------------------------------------*   00040606
       1300-99-FIM.              EXIT.                                  00040706
      *-------------------------------------------------------------*   00040806
                                                                        00040906
      *-------------------------------------------------------------*   00041006
       1400-VERIFICA-FUNC                           SECTION.            00041106
      *-------------------------------------------------------------*   00041206
            EVALUATE SQLCODE                                            00042206
             WHEN 0                                                     00042306
               ADD 1 TO WRK-REGLIDOS                                    00042406
                ADD DB2-SALARIO TO WRK-SALARIO-ACUM                     00042506
               CONTINUE                                                 00042606
             WHEN 100                                                   00042706
              DISPLAY WRK-MSGFIMARQ                                     00042806
             WHEN OTHER                                                 00042906
               MOVE SQLCODE TO WRK-SQLCODE                              00043006
               DISPLAY WRK-MSGERROLEI WRK-SQLCODE                       00043106
             END-EVALUATE.                                              00043206
                                                                        00043306
            PERFORM 2200-CALCULO-SALARIO.                               00043406
      *-------------------------------------------------------------*   00043506
       1400-99-FIM.              EXIT.                                  00043606
      *-------------------------------------------------------------*   00043706
                                                                        00043806
      *-------------------------------------------------------------*   00043906
       2000-PROCESSAR                           SECTION.                00044006
      *-------------------------------------------------------------*   00044106
            MOVE DB2-ID        TO WRK-ID.                               00044306
            MOVE DB2-NOME      TO WRK-NOME.                             00044406
            MOVE DB2-SALARIO   TO WRK-SALARIO.                          00044506
            MOVE DB2-PLANMED   TO WRK-PLANMED.                          00044606
            MOVE DB2-PLANDENT  TO WRK-PLANDENT.                         00044706
            MOVE DB2-COTAS     TO WRK-COTAS.                            00044806
                                                                        00044906
            WRITE FD-SRELBENE FROM WRK-SRELBENE.                        00045006
            ADD 1 TO WRK-REGGRAVADOS                                    00045106
            PERFORM 1300-LER-FUNC.                                      00045406
      *-------------------------------------------------------------*   00045506
       2000-99-FIM.              EXIT.                                  00045606
      *-------------------------------------------------------------*   00045706
                                                                        00045806
      *-------------------------------------------------------------*   00045906
       2200-CALCULO-SALARIO                         SECTION.            00046006
      *-------------------------------------------------------------*   00046106
            IF DB2-SALARIO IS GREATER WRK-MAIOR-SALARIO                 00046506
              MOVE DB2-SALARIO TO WRK-MAIOR-SALARIO                     00046606
            END-IF.                                                     00047006
      *-------------------------------------------------------------*   00047100
       2200-99-FIM.              EXIT.                                  00047200
      *-------------------------------------------------------------*   00047300
                                                                        00047406
      *-------------------------------------------------------------*   00047500
       3000-FINALIZAR                               SECTION.            00047600
      *-------------------------------------------------------------*   00047700
            EXEC SQL                                                    00048006
              CLOSE CFUNC                                               00048106
            END-EXEC.                                                   00048206
            CLOSE SRELBENE.                                             00048406
            PERFORM 3100-DISPLAY-FINAL.                                 00048506
      *-------------------------------------------------------------*   00049300
       3000-99-FIM.              EXIT.                                  00049400
      *-------------------------------------------------------------*   00049500
                                                                        00049606
      *-------------------------------------------------------------*   00049706
       3100-DISPLAY-FINAL                          SECTION.             00049806
      *-------------------------------------------------------------*   00049906
            DISPLAY WRK-MSGREGLIDOS    WRK-REGLIDOS.                    00050606
            DISPLAY WRK-MSGREGGRAVADOS WRK-REGGRAVADOS                  00050706
            DISPLAY WRK-MSGMAIORSAL    WRK-MAIOR-SALARIO.               00050806
            DISPLAY WRK-MSGSALACUM     WRK-SALARIO-ACUM.                00050906
            DIVIDE WRK-SALARIO-ACUM BY WRK-REGLIDOS                     00051006
                  GIVING WRK-MEDIA.                                     00051106
            DISPLAY WRK-MSGMEDIASAL WRK-MEDIA.                          00051206
            DISPLAY WRK-MSGFIMPROG.                                     00051306
      *-------------------------------------------------------------*   00052106
       3100-99-FIM.              EXIT.                                  00052206
      *-------------------------------------------------------------*   00052306
      *-------------------------------------------------------------*   00052400
       9000-TRATAERROS                              SECTION.            00052500
      *-------------------------------------------------------------*   00052600
                                                                        00052700
      *-------------------------------------------------------------*   00052800
       9000-99-FIM.              EXIT.                                  00053000
      *-------------------------------------------------------------*   00060000
